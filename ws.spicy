# Simple parser for the WebSockets protocol
#
#  tested with:
#  printf "\x81\x8f\xb7\xce\xfd\x57\xdf\xab\x91\x3b\xd8\xee\x8a\x32\xd5\xbd\x92\x34\xdc\xab\x89" | spicy-driver ws.spicy
#

module WS;

import Spicy;


export type WS_Message = unit {
        # start with first 2 bytes as a bitfield to get access to bit level flags
        first2B: bitfield(16) {
                fin: 0;
                rsv1: 1;
                rsv2: 2;
                rsv3: 3;
                op: 4..7;
                mask: 8;
                pay1: 9..15;
        } &bitorder = Spicy::BitOrder::MSB0;

        # determine payload length and start point for next field
        switch ( self.first2B.pay1 ) {
                126 -> extpay: ExtPay;
                127 -> extpayc: ExtPayC;
                * -> : void;
        };

        # parse out masking key if payload is masked (from client)
        switch ( self.first2B.mask ) {
                1 -> maskkey: bytes &length=4;
                0 -> : void;
        };

        # grab the payload data. Leave unmasking to bro script
        data: bytes &eod;

        on %done {
                # feedback while coding
                print self;
        }

};

# define this new type which parses out the extended payload length field
type ExtPay = unit {
        pay2: uint<16>;
};

# define this new type which parses out the extended continued payload length field
type ExtPayC = unit {
        pay3: uint<64>;
};
